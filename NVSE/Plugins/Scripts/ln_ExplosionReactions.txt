; Explosion Reactions - Stentorious + Hitman

; On game load
; Reset animation control
Goo1.AuxVarSetFlt "*ExplReact_Anim" 0
SetOnTimerStopHandler 1 (CompileScript "ExplosionReactions\AnimControl.gek") "*ExplReact_Anim"

; On game Restart
if eval Goo1.AuxVarGetFlt "*ExplReact_Init"
	return
endif
Goo1.AuxVarSetFlt "*ExplReact_Init" 1

; Check for requirements
if eval GetNVSEVersion < 6 || (GetNVSEVersion == 6 && GetNVSERevision < 4)
	MessageBoxEx "Explosion Reactions missing requirement!%rInstall xNVSE 6.4.0+."
	return
endif
if GetPluginVersion "JohnnyGuitarNVSE" < 360
	MessageBoxEx "Explosion Reactions missing requirement!%rInstall latest JohnnyGuitar NVSE plugin."
	return
endif
if GetPluginVersion "ShowOffNVSE Plugin" < 165
	MessageBoxEx "Explosion Reactions missing requirement!%rInstall latest ShowOff NVSE plugin."
	return
endif
if GetPluginVersion "lStewieAl's Tweaks" < 905
	MessageBoxEx "Explosion Reactions missing requirement!%rInstall lStewieAl's Tweaks 9.05+."
	return
endif
if GetPluginVersion "kNVSE" < 37
	MessageBoxEx "Explosion Reactions missing requirement!%rInstall latest kNVSE plugin."
	return
endif

; Load INI settings
Goo1.AuxVarSetFlt "*ExplReact_INI" (Clamp (GetINIFloat_Cached "General:iRadius" "Stentorious\ExplosionReactions.ini" 80) 0 100) 0
Goo1.AuxVarSetFlt "*ExplReact_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:iHeadingAngle" "Stentorious\ExplosionReactions.ini" 50)) 1
Goo1.AuxVarSetFlt "*ExplReact_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:iMinForce" "Stentorious\ExplosionReactions.ini" 1)) 2
Goo1.AuxVarSetFlt "*ExplReact_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:iMinDamage" "Stentorious\ExplosionReactions.ini" 1)) 3
Goo1.AuxVarSetFlt "*ExplReact_INI" (GetMaxOf 0 (GetINIFloat_Cached "General:fCooldown" "Stentorious\ExplosionReactions.ini" 2.5)) 4
Goo1.AuxVarSetFlt "*ExplReact_INI" (eval GetINIFloat_Cached "General:bForceWalk" "Stentorious\ExplosionReactions.ini" 0) 5
Goo1.AuxVarSetFlt "*ExplReact_INI" (eval GetINIFloat_Cached "General:bForceFirstPerson" "Stentorious\ExplosionReactions.ini" 0) 6
Goo1.AuxVarSetFlt "*ExplReact_INI" (eval GetINIFloat_Cached "General:bPlayerExplosions" "Stentorious\ExplosionReactions.ini" 1) 7
Goo1.AuxVarSetFlt "*ExplReact_INI" (eval GetINIFloat_Cached "General:bNPCExplosions" "Stentorious\ExplosionReactions.ini" 1) 8
Goo1.AuxVarSetFlt "*ExplReact_INI" (eval GetINIFloat_Cached "General:bPowerArmor" "Stentorious\ExplosionReactions.ini" 1) 9

; Whitelist keywords
if eval IsModLoaded "LonesomeRoad.esm"
	AssignKeyword (EditorIDToFormID "NVDLC04FlashBangExplosion") "ExplReactWhitelist"
endif

; Blacklist keywords
;AssignKeyword DLC03FlamerFireBurst "ExplReactBlacklist"

; Event handlers
SetEventHandler "ShowOff:OnExplosionHit" (CompileScript "ExplosionReactions\OnHit.gek") "first"::Player

CloseFileSO "Stentorious\ExplosionReactions.ini" 0
